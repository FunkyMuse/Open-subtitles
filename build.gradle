
buildscript {

    ext {
        retrofit = "2.9.0"
        moshi = "1.12.0"
        coroutines = '1.5.2'

        utilsVersion = "3.1.5"

        daggerVersion = '2.40'
        hiltJetpackVersion = '1.0.0'

        kotlin_version = '1.5.31'

        compileVersion = 31
        minVersion = 21
        verCode = 1
        verName = "1.0.0"
        testRunner = "androidx.test.runner.AndroidJUnitRunner"

    }


    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath "com.google.dagger:hilt-android-gradle-plugin:$daggerVersion"
    }
}


allprojects {
    group = "io.github.funkymuse"

    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


subprojects {


    switch (it.name) {
        case 'app':
            apply plugin: 'com.android.application'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-parcelize'
            apply plugin: 'kotlin-kapt'
            apply plugin: 'dagger.hilt.android.plugin'
            dependencies {
                implementation fileTree(dir: 'libs', include: ['*.jar'])

                implementation "com.github.FunkyMuse.KAHelpers:retrofit:$utilsVersion"
                implementation "com.github.FunkyMuse.KAHelpers:recyclerview:$utilsVersion"

                //moshi
                implementation "com.squareup.moshi:moshi-kotlin:$moshi"
                implementation "com.squareup.retrofit2:converter-moshi:$retrofit"

                kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi"

                //dagger
                implementation "com.google.dagger:dagger:$daggerVersion"
                kapt "com.google.dagger:dagger-compiler:$daggerVersion"

                //hilt
                implementation "com.google.dagger:hilt-android:$daggerVersion"
                kapt "com.google.dagger:hilt-android-compiler:$daggerVersion"

                //hilt jetpack
                implementation "androidx.hilt:hilt-navigation-fragment:$hiltJetpackVersion"
                kapt "androidx.hilt:hilt-compiler:$hiltJetpackVersion"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"

            }
            break

        case ['opensubshilt', 'opensubshiltsealed']:
            apply plugin: 'com.android.library'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-kapt'
            apply plugin: 'dagger.hilt.android.plugin'
            apply plugin: 'com.github.dcendents.android-maven'
            dependencies {
                implementation "com.github.FunkyMuse.KAHelpers:retrofit:$utilsVersion"

                //moshi
                implementation "com.squareup.moshi:moshi-kotlin:$moshi"
                implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
                kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi"

                //dagger
                implementation "com.google.dagger:dagger:$daggerVersion"
                kapt "com.google.dagger:dagger-compiler:$daggerVersion"

                //hilt
                implementation "com.google.dagger:hilt-android:$daggerVersion"
                kapt "com.google.dagger:hilt-android-compiler:$daggerVersion"

                //hilt jetpack
                implementation "androidx.hilt:hilt-navigation-fragment:$hiltJetpackVersion"
                kapt "androidx.hilt:hilt-compiler:$hiltJetpackVersion"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
            }
            break

        case ['opensubs', 'opensubssealed']:
            apply plugin: 'com.android.library'
            apply plugin: 'kotlin-android'
            apply plugin: 'kotlin-kapt'
            apply plugin: 'com.github.dcendents.android-maven'
            dependencies {
                implementation "com.github.FunkyMuse.KAHelpers:retrofit:$utilsVersion"

                //moshi
                implementation "com.squareup.moshi:moshi-kotlin:$moshi"
                implementation "com.squareup.retrofit2:converter-moshi:$retrofit"
                kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
            }
            break

    }


    android {
        compileSdkVersion compileVersion

        defaultConfig {
            minSdkVersion minVersion
            targetSdkVersion compileVersion
            versionCode verCode
            versionName verName
            testInstrumentationRunner testRunner
        }

        compileOptions {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }

        kotlinOptions {
            jvmTarget = "1.8"
        }

        buildFeatures {
            aidl = false
            renderScript = false
            resValues = false
            shaders = false
            buildConfig = false
        }

        testOptions.unitTests {
            includeAndroidResources = true
        }
    }

}